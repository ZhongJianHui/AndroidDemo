apply plugin: 'com.android.library'

android {
    compileSdkVersion COMPILE_SDK_VERSION as int
    defaultConfig {
        minSdkVersion MIN_SDK_VERSION as int
    }
}


apply plugin: 'maven-publish'

def output = 'build/outputs/aar'
def outputJarName = getLibraryName()



publishing {
    repositories {
        maven {
            url '../maven/'
        }
    }

    publications {
        mavenRelease(MavenPublication) {
            def type = 'release'
            def targetFile = file("${output}/${outputJarName}-${type}.aar")

            if (targetFile.exists()) {
                groupId GROUP
                artifactId outputJarName
                version VERSION_NAME
                artifact source: targetFile, extension: "aar"
            } else {
                println "error - $outputJarName $type aar 不存在"
            }
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each { dependency ->
                    handleCompileDependencies(dependency, dependenciesNode)
                }
                configurations.compile.allDependencies.each { dependency ->
                    handleCompileDependencies(dependency, dependenciesNode)
                }
                configurations.api.allDependencies.each { dependency ->
                    handleCompileDependencies(dependency, dependenciesNode)
                }
            }
        }
    }
}

def handleCompileDependencies(dependencies, dependencyNodes) {
    if (null != dependencies.group) {
        def dependencyNode = dependencyNodes.appendNode('dependency')
        def groupId = dependencies.group
        def artifactId = dependencies.name
        def version = dependencies.version
        dependencyNode.appendNode('groupId', groupId)
        dependencyNode.appendNode('artifactId', artifactId)
        dependencyNode.appendNode('version', version)
    }
}